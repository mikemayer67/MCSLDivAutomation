#!/usr/bin/env perl
use strict;
use warnings;

use FindBin;
use lib $FindBin::RealBin;

use Data::Dumper;
use DBI;
use FileHandle;
use List::Util qw(sum);
use Scalar::Util qw(looks_like_number);

use DivDB::SDIF;
use DivDB::CarnivalEvents;
use DivDB::Events;
use DivDB::Teams;
use DivDB::Schedule;
use DivDB::Swimmers;
use DivDB::TeamSeed;

my $filename = shift @ARGV;

die "\nUsage:: $FindBin::Script cl2_filename\n\n" unless defined $filename;
die "\nSorry:: filename must end with .cl2\n\n" unless $filename=~/\.cl2$/;

my $cl2 = new FileHandle($filename);
die "\nSorry:: Failed to open $filename\n   $!\n\n" unless $cl2;

my $dbh = &DivDB::getConnection;

my $sdif_codes = new DivDB::SDIF;
my $events     = new DivDB::Events;
my $teams      = new DivDB::Teams;
my $swimmers   = new DivDB::Swimmers;
my $schedule   = new DivDB::Schedule;
my $seed       = new DivDB::TeamSeed;

my $A0;
my $B1;
my $Z0;
my $curTeamCode;
my $curRelay;
my %num;
my @dates;
my @teams;
my %score;

my $meet   = $schedule->divisionals_meet;
my $nteams = $seed->count;

my %results;
my %ties;

my %CL2;
while(<$cl2>)
{
  next unless /^([A-Z]\d)/;
  my $code = $1;

  die "\nFirst record must be A0\n\n" unless defined $A0 || $code eq 'A0';
  die "\nOnly one A0 record allowed\n\n" if defined $A0 && $code eq 'A0';
  die "\nOnly one B1 record allowed\n\n" if defined $B1 && $code eq 'B1';
  die "\nNo records should follow Z0\n\n" if defined $Z0;

  my $class = "CL2::$code";
  unless(exists $CL2{$code})
  {
    eval "use $class;";
    die "\nFailed to load $class\n  $@\n\n" if $@;
    $CL2{$code} = 1;
  }

  my $rec = $class->new($_);

  if($code eq 'A0')
  {
    $A0 = $rec;
  }
  elsif($code eq 'B1')
  {
    $B1 = $rec;
    $num{B}++;
    @dates = ($rec->{start_date},$rec->{end_date});
  }
  elsif($code eq 'C1')
  {
    $teams->verify_CL2($rec);
    $curTeamCode = $rec->{team_code};
    push @teams, $curTeamCode;
    die "\nOnly $nteams teams allowed in a divisional meet\n\n" if @teams>$nteams;

    $num{C}++;
  }
  elsif($code eq 'D0')
  {
    die "\n D0 record requires prior C1 record\n\n" unless defined $curTeamCode;
    $rec->{team} = $curTeamCode;
    $rec->{relay} = 'N';
    $swimmers->verify_CL2($rec);
    $events->verify_CL2($rec);

    my $number = $rec->{evt_number};

    my $points = $rec->{points};
    $points = 0 unless defined $points && looks_like_number($points);
    my $dq = $rec->{finals_time}=~/dq|dnf/i;
    my $ns = $rec->{finals_time}=~/ns/i;
    $score{$curTeamCode} += $points;
    my $place = $rec->{finals_rank};

    unless($ns)
    {
      my @values = ( $meet,
                     "'$rec->{ussid}'",
                     $number,
                     $rec->{finals_heat},
                     $rec->{finals_lane},
                     cvt_time($rec->{seed_time})||'NULL',
                     $dq ? "'Y'" : "'N'",
                     cvt_time($rec->{finals_time})||'NULL',
                     $place||'NULL',
                     $dq ? 'NULL' : $points );
      my $values = join ',', @values;
      my $sql = "delete from individual_results where meet=$meet and swimmer='$rec->{ussid}' and event=$number";
      $dbh->do($sql);
      $sql = "insert into individual_results values ($values)";
      $dbh->do($sql) unless $ns;

      unless($dq)
      {
        if(exists $results{$number}{$place})
        {
          push @{$ties{$number}{$place}}, $results{$number}{$place}
            unless exists $ties{$number}{$place};
          push @{$ties{$number}{$place}}, [$rec->{ussid},$points];
        }
        else
        {
          $results{$number}{$place} = [$rec->{ussid},$points];
        }
      }
    }

    $num{D}++;
  }
  elsif($code eq 'D3')
  {
    $swimmers->verify_ussnum($rec);
  }
  elsif($code eq 'E0')
  {
    die "\nE0 record requires prior C1 record\n\n" unless defined $curTeamCode;
    $rec->{relay} = 'Y';
    $events->verify_CL2($rec);

    my $number = $rec->{evt_number};

    my $points = $rec->{points};
    $points = 0 unless defined $points && looks_like_number($points);
    my $dq = $rec->{finals_time}=~/dq|dnf/i;
    my $ns = $rec->{finals_time}=~/ns/i;
    $score{$curTeamCode} += $points;

    unless($ns)
    {
      my $place = $rec->{finals_rank};
      my @values = ( $meet,
                     "'$curTeamCode'",
                     $number,
                     "'A'",
                     'NULL','NULL','NULL','NULL',
                     $rec->{finals_heat},
                     $rec->{finals_lane},
                     0,
                     cvt_time($rec->{seed_time})||'NULL',
                     $dq ? "'Y'" : "'N'",
                     cvt_time($rec->{finals_time})||'NULL',
                     $place||'NULL',
                     $dq ? 'NULL' : $points );
      my $values = join ',', @values;
      my $sql = "delete from relay_results where meet=$meet and team='$curTeamCode' and relay_team='A' and event=$number";
      $dbh->do($sql);
      $sql = "insert into relay_results values ($values)";
      $dbh->do($sql);

      unless($dq)
      {
        if(exists $results{$number}{$place})
        {
          push @{$ties{$number}{$place}}, $results{$number}{$place} 
            unless exists $ties{$number}{$place};
          push @{$ties{$number}{$place}}, [$curTeamCode,$points];
        }
        else
        {
          $results{$number}{$place} = [$curTeamCode,$points];
        }
      }
    }
    $num{E}++;
    $curRelay = $ns ? 'ns' : $rec;
  }
  elsif($code eq 'F0')
  {
    die "\nF0 record requires prior E0 record\n\n" unless defined $curRelay;
    $rec->{team} = $rec->{team_code};
    $swimmers->verify_CL2($rec);

    my $ussid = $rec->{ussid};
    my $leg   = $rec->{finals_leg};
    if($leg=~/^[1234]$/)
    {
      $dbh->do("update relay_results set swimmer$leg='$ussid',total_age=total_age+$rec->{age} where meet=$meet and team='$curTeamCode' and event=$curRelay->{evt_number} and relay_team='A'");
    }

    $num{F}++;
  }
  elsif($code eq 'G0')
  {
    $num{G}++;
  }
  elsif($code eq 'Z0')
  {
    foreach (qw/B C D E F/)
    {
      warn "Mismatch in $_ count (Z0 says $rec->{num_$_} / found $num{$_})\n"
        unless $rec->{"num_$_"} == $num{$_};
    }
    $Z0 = $rec;
  }
  else
  {
    die Dumper($rec);
  }

  $curRelay = undef unless $code=~/^[EFG]/;
}

foreach my $event_number (keys %ties)
{
  my $event = $events->{$event_number};

  if($event->isRelay)
  {
    my @points = ( $nteams == 6 ? (0,28,20,16,12,8,4) :
                   $nteams == 7 ? (0,32,28,20,16,12,8,4) :
                   die "need to add points for $nteams teams\n" );

    foreach my $place (keys %{$ties{$event_number}})
    {
      my $swimmers = $ties{$event_number}{$place};
      my $nswimmers = @$swimmers;
      my @pts = splice @points, $place, $nswimmers;
      my $true_pts = (sum @pts)/$nswimmers;
      print "\n";
      foreach my $s (@$swimmers)
      {
        my($team,$pts) = @$s;
        my $delta = $true_pts-$pts;
        print " Correcting tie points ($event_number/$place): $team ($true_pts vs $pts)\n";
        $dbh->do("update relay_results set points=$true_pts where meet=$meet and team='$team' and event=$event_number and relay_team='A'");
        $score{$team} += $delta;
      }
    }
  }
  else
  {
    my @points = ( $nteams == 6 ? (0, 16, 13,12,11,10,9, 7, 5,4,3,2,1) :
                   $nteams == 7 ? (0, 18, 15,14,13,12,11,10, 8, 6,5,4,3,2,1) :
                   die "need to add points for $nteams $teams\n" );
    foreach my $place (keys %{$ties{$event_number}})
    {
      my @swimmers = @{$ties{$event_number}{$place}};
      my $nswimmers = @swimmers;
      my @pts = splice @points, $place, $nswimmers;
      my $true_pts = (sum @pts)/$nswimmers;
      print "\n";
      foreach my $s (@swimmers)
      {
        my($ussid,$pts) = @$s;
        my $team = $swimmers->{$ussid}{team};
        my $delta = $true_pts-$pts;
        print " Correcting tie points ($event_number/$place): $ussid/$team ($true_pts vs $pts)\n";
        $dbh->do("update individual_results set points=$true_pts where meet=$meet and swimmer='$ussid' and event=$event_number");
        $score{$team} += $delta;
      }
    }
  }
}

die "\nDivisionals meet must have $nteams team (C1) records\n\n" unless @teams==$nteams;
my @ranked_teams = sort { $score{$b} <=> $score{$a} } @teams;
my $lastScore=0;
my @points = (20,16,12,8,4,0,0); 
$dbh->do("delete from divisionals");
foreach my $team (@ranked_teams)
{
  my $points = shift @points;
  $dbh->do("insert into divisionals values ('$team','$dates[0]','$dates[1]',$score{$team},$points)");
}

print "\nNumber of Swimmers: $Z0->{num_swimmers}\n";
print "\nFinal Score\n";
foreach my $team (@ranked_teams)
{
  print "  $score{$team}  $teams->{$team }{team_name}\n";
}
print "\n";




################################################################################


sub cvt_time
{
  my $time = shift;
  return undef unless $time=~/^\s*(?:(\d+):)?(\d+(?:\.\d+)?)\s*$/;
  my $sec = $2;
  $sec += 60*$1 if defined $1;
  return $sec;
}
