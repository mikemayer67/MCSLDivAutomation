#!/usr/bin/env perl
use strict;
use warnings;

use FindBin;
use lib $FindBin::RealBin;

use Data::Dumper;
use DBI;
use FileHandle;

use DivDB;
use DivDB::SDIF;
use DivDB::Meets;
use DivDB::Points;
use DivDB::Results;
use DivDB::Relays;
use DivDB::Stats;
use DivDB::CarnivalRelays;
use DivDB::Divisionals;
use DivDB::Eligible;

my $sdif     = new DivDB::SDIF;
my $meets    = new DivDB::Meets;
my $points   = new DivDB::Points;
my $results  = new DivDB::Results;
my $relays   = new DivDB::Relays;
my $stats    = new DivDB::Stats;
my $carnival = new DivDB::CarnivalRelays;
my $div      = new DivDB::Divisionals;
my $eligible = new DivDB::Eligible;
my $teams    = new DivDB::Teams;

my $htmldir = $FindBin::RealBin;
$htmldir=~s(/[^/]+$)(/html);

my $tmpl = join '', <DATA>;

$tmpl=~s/\{\{YEAR}}/$DivDB::Year/g;
$tmpl=~s/\{\{DIVISION}}/$DivDB::Division/g;

&gen_summary;
&gen_individual;
&gen_team($_) foreach keys %{$teams};
&gen_relay;
&gen_carnival;
&gen_stats;
&gen_eligible;

################################################################################
# common html setup
################################################################################

sub setup_html
{
  my $page = shift;

  my $html = $tmpl;
  my $date = localtime;
  $html=~s/\{\{DATE}}/$date/g;

  my @pages  = qw(summary individual relay carnival stats eligible);
  my @labels = ('Summary','Individual Results','Relay Results','Relay Carnival','Statistics','Divisional Eligibility');
  my %labels = map { ( $pages[$_] => $labels[$_]||$pages[$_] ) } (0..$#pages);

  my @links = map { my $tag = $_ eq $page ? 'span' : 'a' ;
                    "<$tag class=menubar href=$_.html>$labels{$_}</$tag>" 
                  } @pages;

  my $label = $labels{$page} || $teams->{$page}{team_name};
  $html=~s/\{\{TITLE}}/$label/g;

  while($html=~/^(.*?)\{\{LINKS}}/m)
  {
    my $prefix = $1;
    $prefix=~s/./ /g;

    my $links = join "\n$prefix", @links;
      
    $html=~s/\{\{LINKS}}/$links/;
  }

  return $html;
}

################################################################################
# gen summary page
################################################################################

sub gen_summary
{
  my $html = setup_html('summary');

  my $fh = new FileHandle(">$htmldir/summary.html") || die "Failed to create summary.html: $!\n";

  my $points_summary = $points->gen_html;
  my $meets_summary  = $meets->gen_html;
  my $div_summary    = $div->gen_html;

  $html=~s/(?=\{\{BODY}})/$points_summary/g;
  $html=~s/(?=\{\{BODY}})/$meets_summary/g;
  $html=~s/(?=\{\{BODY}})/$div_summary/g;

  $html=~s/\{\{BODY}}//g;

  print $fh $html;
  $fh->close;
}


################################################################################
# gen individual page
################################################################################

sub gen_individual
{
  my $html = setup_html('individual');

  my $fh = new FileHandle(">$htmldir/individual.html") || die "Failed to create individual.html: $!\n";

  my $body = $results->gen_html;
  
  $html=~s/\{\{BODY}}/$body/g;

  print $fh $html;
  $fh->close;
}

sub gen_team
{
  my($team) = @_;

  my $html = setup_html($team);

  my $fh = new FileHandle(">$htmldir/$team.html") || die "Failed to create $team.html: $!\n";

  my $body = $results->gen_html($team);
  
  $html=~s/\{\{BODY}}/$body/g;

  print $fh $html;
  $fh->close;
}

################################################################################
# gen relay page
################################################################################

sub gen_relay
{
  my $html = setup_html('relay');

  my $fh = new FileHandle(">$htmldir/relay.html") || die "Failed to create relay.html: $!\n";

  my $body = $relays->gen_html;
  
  $html=~s/\{\{BODY}}/$body/g;

  print $fh $html;
  $fh->close;
}

################################################################################
# gen relay page
################################################################################

sub gen_carnival
{
  my $html = setup_html('carnival');

  my $fh = new FileHandle(">$htmldir/carnival.html") || die "Failed to create carnival.html: $!\n";

  my $body = $carnival->gen_html;
  
  $html=~s/\{\{BODY}}/$body/g;

  print $fh $html;
  $fh->close;
}

################################################################################
# gen stats page
################################################################################

sub gen_stats
{
  my $html = setup_html('stats');

  my $fh = new FileHandle(">$htmldir/stats.html") || die "Failed to create stats.html: $!\n";

  my $body = $stats->gen_html;

  $html=~s/\{\{BODY}}/$body/g;

  print $fh $html;
  $fh->close;
}

################################################################################
# gen eligibility page
################################################################################

sub gen_eligible
{
  my $html = setup_html('eligible');

  my $fh = new FileHandle(">$htmldir/eligible.html") || die "Failed to create eligible.html: $!\n";

  my $body = $eligible->gen_html;

  $html=~s/\{\{BODY}}/$body/g;

  print $fh $html;
  $fh->close;

}


__DATA__
<html>
  <head>
    <meta http-equiv="Cache-Control" content="no-cache, no-store, must-revalidate" />
    <meta http-equiv="Pragma" content="no-cache" />
    <meta http-equiv="Expires" content="0" />
    <title>MCSL/Div {{DIVISION}}/{{YEAR}} - {{TITLE}}</title>
    <link rel=stylesheet type='text/css' href='div.css'>
  </head>
  <body>
    <table id=layout>
      <tr id=header>
        <td><img id=logo src='images/mcsl.jpg'>
          <td><h1 id=title>Montgomery County Swim League</h1>
            <h2 id=subtitle>Division {{DIVISION}} - {{YEAR}}</h2>
              <div id=date>Report Generated {{DATE}}</div>
      </tr>
      <tr id=links>
        <td></td>
        <td id=links>
          {{LINKS}}
        </td>
      </tr>
      <tr><td></td>
        <td>
          {{BODY}}
        </td>
      </tr>
      <tr class=footer><td></td>
        <td class=footer>
          <div id=contact>Contact <a href='mailto:mikemayer67@vmwishes.com'>Mike Mayer</a> with any comments/suggestions</div>

        </td>
      </tr>
    </table>
  </body>
</html>
